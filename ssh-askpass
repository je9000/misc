#!/usr/bin/env osascript
# Copyright (c) 2023-, John Eaglesham
# Copyright (c) 2014-, Johan Carlquist <johan@rymdvarel.se>
# Copyright (c) 2011-, Simon Lundstr√∂m <simmel@soy.se>
#
# Permission to use, copy, modify, and/or distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
# REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND
# FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
# INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
# LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR
# OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
# PERFORMANCE OF THIS SOFTWARE.

# Forked by John from https://github.com/theseal/ssh-askpass/blob/master/ssh-askpass
# Put ssh-askpass.plist in ~/Library/LaunchAgents/
# run launchctl load -w ~/Library/LaunchAgents/ssh-askpass.plist
# ssh-add -c your key
# you might want to enable "Use keyboard navigation to move focus between controls"
# in the os settings

on run argv
    set args to argv as text
    set frontmost_application to name of (info for (path to frontmost application))
    tell application frontmost_application
        if args ends with ": " or args ends with ":" then
            if args contains "pass" or args contains "pin" then
                display dialog args with icon caution default button "OK" default answer "" with hidden answer
            else
                display dialog args with icon caution default button "OK" default answer ""
            end if
            return result's text returned
        else if args ends with "Are you sure you want to continue connecting (yes/no/[fingerprint])? " then
            display dialog args with icon caution buttons {"no", "yes", "fingerprint"} default button "no" default answer ""
            set dialog_result to result
            if dialog_result's button returned = "fingerprint" then
                return dialog_result's text returned
            end if
            return dialog_result's button returned
        else
            display dialog args with icon caution default button "Cancel"
            return
        end if
    end tell
end run
